plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.netflix.dgs.codegen' version '7.0.3'
    id 'checkstyle'
    id 'com.diffplug.spotless' version '6.25.0'
}

group = 'loandly'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

spotless {
    java {
        eclipse()
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
        removeUnusedImports()
        importOrder(
                'java',
                'javax',
                'org',
                'com',
                '',
                'static'
        )
        target 'src/**/*.java'
    }
}

checkstyle {
    toolVersion = '10.23.0' // 원하는 버전 사용
    configFile = file("config/checkstyle/checkstyle.xml") // 설정 파일 경로
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = false
        html.required = true // HTML 리포트로 보기 좋게
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'

    //heart beat check
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    //jpa
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    //lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    //mysql
    runtimeOnly 'com.mysql:mysql-connector-j'

    //h2
    runtimeOnly 'com.h2database:h2'

    //test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    //graphql
    implementation 'org.springframework.boot:spring-boot-starter-graphql'
    testImplementation 'org.springframework.graphql:spring-graphql-test'
    implementation 'com.graphql-java:graphql-java-extended-scalars:22.0'

    //security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    //jwt
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    //redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-cache'

    //validate
    implementation 'org.springframework.boot:spring-boot-starter-validation'
}

generateJava {
    schemaPaths = ["${projectDir}/src/main/resources/graphql-client"]
    packageName = 'loandly.loandly.codegen'
    generateClient = true
}

tasks.named('test') {
    useJUnitPlatform()
}
